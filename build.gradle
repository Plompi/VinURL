plugins {
	id "fabric-loom" version "1.9-SNAPSHOT"
	id "org.moddedmc.wiki.toolkit" version "0.2.5"
}

def mod_version = file('CHANGELOG.md').text.find(/Version: (.+)/) { _, version -> version }
version = "${mod_version}+mc${mod_minecraft_version_end}"

base {
	archivesName = mod_id
}

repositories {
	maven { url "https://maven.wispforest.io" }
	// Add repositories to retrieve artifacts from in here.
	// You should only use this when depending on other mods because
	// Loom adds the essential maven repositories to download Minecraft and libraries from automatically.
	// See https://docs.gradle.org/current/userguide/declaring_repositories.html
	// for more information about repositories.
}

dependencies {
	implementation "net.lingala.zip4j:zip4j:2.11.5"
	annotationProcessor modImplementation("io.wispforest:owo-lib:${project.run_owo_version}")
	minecraft "com.mojang:minecraft:${project.run_minecraft_version}"
	mappings "net.fabricmc:yarn:${project.run_yarn_mappings}:v2"
	modImplementation "net.fabricmc:fabric-loader:${project.run_fabric_loader_version}"
	modImplementation "net.fabricmc.fabric-api:fabric-api:${project.run_fabric_version}"
}

processResources {
	var replaceProperties = [
			mod_version               		: mod_version,
			mod_id							: mod_id,
			mod_name						: mod_name,
			mod_license                     : mod_license,
			mod_author                      : mod_author,
			mod_credits                     : mod_credits,
			mod_description                 : mod_description,
			mod_url                         : mod_url,

			mod_minecraft_version_start		: mod_minecraft_version_start,
			mod_minecraft_version_end		: mod_minecraft_version_end,

			mod_fabric_loader_version		: mod_fabric_loader_version,
			mod_neoforge_loader_version		: mod_neoforge_loader_version
	]
	inputs.properties replaceProperties
	filesMatching(['fabric.mod.json', 'META-INF/neoforge.mods.toml']) {
		expand replaceProperties
	}
}

tasks.withType(JavaCompile).configureEach {
	it.options.release = 21
}

java {
	sourceCompatibility = JavaVersion.VERSION_21
	targetCompatibility = JavaVersion.VERSION_21
}

jar {
	// Wenn NeoForge nicht gebaut wird, schlie√üt das META-INF-Verzeichnis aus
	if (!project.hasProperty('neoforge')) {
		exclude('META-INF/**')
	}
	from("LICENSE") {
		rename { "${it}_${mod_id}" }
	}
}

wiki {
	docs {
		vinurl {
			root = file("docs")
			wikiAccessToken = System.getenv('WIKI_TOKEN')
		}
	}
}